/* cowbel test suite
 *
 * Written in 2012 by David Given.
 *
 * To the extent possible under law, the author has dedicated all copyright
 * and related and neighboring rights to this software to the public domain
 * worldwide. This software is distributed without any warranty.
 *
 * Please see the file COPYING.CC0 in the distribution package for more
 * information.
 */

var width = 10;
var height = 10;

/* Nasty hack to set the size of an array. */
var buffera = [[0]]; buffera.resize(height, [0]);
var bufferb = [[0]]; bufferb.resize(height, [0]);
var front = buffera;
var back = bufferb;

function print(s: string)
{
	s.print();
}

function wipe_buffers()
{
	for y = 0, height
	{
		var rowa = [0]; rowa.resize(width, 0);
		buffera.set(y, rowa);
		var rowb = [0]; rowb.resize(width, 0);
		bufferb.set(y, rowb);

		for x = 0, width		
		{
			rowa.set(x, 0);
			rowb.set(x, 0);
		}
	}
}

function getxy(x: int, y: int): int
{
	return front.get(y).get(x);
}

function setxy(x: int, y: int, value: int)
{
	front.get(y).set(x, value);
}
	
function populate_front()
{
	setxy(2, 4, 1);
	setxy(3, 4, 1);
	setxy(4, 4, 1);
	setxy(2, 3, 1);
	setxy(3, 2, 1);
}

function draw_front()
{
	for y = 0, height
	{
		var row = front.get(y);
		var s = "";

		for x = 0, width		
		{
			var cell = row.get(x);
			if (cell == 0)
				s = s + ".";
			else
				s = s + "*";
		}
		print(s);
	}
}

function do_cell(x: int, y: int)
{
	var s = 
		getxy(x-1, y-1) +
		getxy(x-1, y  ) +
		getxy(x-1, y+1) +
		getxy(x  , y-1) +
		getxy(x  , y+1) +
		getxy(x+1, y-1) +
		getxy(x+1, y  ) +
		getxy(x+1, y+1);
		
	var old = getxy(x, y);
	
	if (s == 2)
	{
		var v = getxy(x, y);
		back.get(y).set(x, v);
	}
	else if (s == 3)
		back.get(y).set(x, 1);
	else
		back.get(y).set(x, 0);
}

function process()
{
	var maxw = width - 1;
	var maxh = height - 1;

	for y = 1, maxh	
	{
		for x = 1, maxw
			do_cell(x, y);
	}
}
	 
function run()
{
	print("Initialising. (Be patient...)");
	wipe_buffers();
	populate_front();

	var b = true;
	for generations = 0, 10
	{
		print("");
		print("Frame " + generations.toString());
		process();
		
		if (b)
		{
			front = bufferb;
			back = buffera;
		}
		else
		{
			front = buffera;
			back = bufferb;
		}
		b = !b;
		
		draw_front();
	}
}

run();
